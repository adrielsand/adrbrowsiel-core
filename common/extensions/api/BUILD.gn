import("//adrbrowsiel/components/binance/browser/buildflags/buildflags.gni")
import("//adrbrowsiel/components/adrbrowsiel_together/buildflags/buildflags.gni")
import("//adrbrowsiel/components/adrbrowsiel_wallet/common/buildflags/buildflags.gni")
import("//adrbrowsiel/components/crypto_dot_com/browser/buildflags/buildflags.gni")
import("//adrbrowsiel/components/gemini/browser/buildflags/buildflags.gni")
import("//adrbrowsiel/components/ipfs/buildflags/buildflags.gni")
import("//adrbrowsiel/components/ftx/browser/buildflags/buildflags.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/json_schema_compiler/json_features.gni")
import("//tools/json_schema_compiler/json_schema_api.gni")
json_features("api_features") {
  feature_type = "APIFeature"
  method_name = "AddadrbrowsielAPIFeatures"
  sources = [
    "//chrome/common/extensions/api/_api_features.json",
    "//extensions/common/api/_api_features.json",
    "_api_features.json",
  ]

  if (adrbrowsiel_wallet_enabled) {
    sources += [ "_adrbrowsiel_wallet_api_features.json" ]
  }

  if (ipfs_enabled) {
    sources += [ "_ipfs_api_features.json" ]
  }

  if (binance_enabled) {
    sources += [ "_binance_api_features.json" ]
  }

  if (gemini_enabled) {
    sources += [ "_gemini_api_features.json" ]
  }

  if (enable_ftx) {
    sources += [ "_ftx_api_features.json" ]
  }

  if (crypto_dot_com_enabled) {
    sources += [ "_crypto_dot_com_features.json" ]
  }

  if (adrbrowsiel_together_enabled) {
    sources += [ "_adrbrowsiel_together_api_features.json" ]
  }
}

json_features("permission_features") {
  feature_type = "PermissionFeature"
  method_name = "AddadrbrowsielPermissionFeatures"
  sources = [
    "//chrome/common/extensions/api/_permission_features.json",
    "//extensions/common/api/_permission_features.json",
    "_permission_features.json",
  ]
}

json_features("manifest_features") {
  feature_type = "ManifestFeature"
  method_name = "AddadrbrowsielManifestFeatures"
  sources = [
    "//chrome/common/extensions/api/_manifest_features.json",
    "//extensions/common/api/_manifest_features.json",
    "_manifest_features.json",
  ]
}

json_features("behavior_features") {
  feature_type = "BehaviorFeature"
  method_name = "AddadrbrowsielBehaviorFeatures"
  sources = [ "//extensions/common/api/_behavior_features.json" ]
}

adrbrowsiel_extensions_api_schema_sources = [
  "adrbrowsiel_rewards.json",
  "adrbrowsiel_shields.json",
  "adrbrowsiel_theme.json",
  "adrbrowsiel_today.json",
  "greaselion.json",
  "rewards_notifications.json",
]

if (ipfs_enabled) {
  adrbrowsiel_extensions_api_schema_sources += [ "ipfs.json" ]
}

if (adrbrowsiel_wallet_enabled) {
  adrbrowsiel_extensions_api_schema_sources += [ "adrbrowsiel_wallet.json" ]
}

if (binance_enabled) {
  adrbrowsiel_extensions_api_schema_sources += [ "binance.json" ]
}

if (gemini_enabled) {
  adrbrowsiel_extensions_api_schema_sources += [ "gemini.json" ]
}

if (enable_ftx) {
  adrbrowsiel_extensions_api_schema_sources += [ "ftx.json" ]
}

if (crypto_dot_com_enabled) {
  adrbrowsiel_extensions_api_schema_sources += [ "crypto_dot_com.json" ]
}

if (adrbrowsiel_together_enabled) {
  adrbrowsiel_extensions_api_schema_sources += [ "adrbrowsiel_together.json" ]
}

adrbrowsiel_extensions_api_uncompiled_sources = []
adrbrowsiel_extensions_api_root_namespace = "extensions::api::%(namespace)s"
adrbrowsiel_extensions_api_schema_include_rules =
    "//adrbrowsiel/common/extensions/api:extensions::api::%(namespace)s"

adrbrowsiel_extensions_api_deps = [
  ":api_features",
  ":behavior_features",
  ":manifest_features",
  ":permission_features",
  ":resources",
  "//chrome/common/extensions/api",
  "//extensions/common/api",
  "//skia",
]

grit("resources") {
  source = "adrbrowsiel_api_resources.grd"
  outputs = [
    "grit/adrbrowsiel_api_resources.h",
    "adrbrowsiel_api_resources.pak",
  ]
  resource_ids = "//adrbrowsiel/browser/resources/resource_ids"
}

function_registration("generated_api_registration") {
  sources = adrbrowsiel_extensions_api_schema_sources
  impl_dir = "//adrbrowsiel/common/extensions/api/"
  bundle_name = "adrbrowsiel"
  root_namespace = adrbrowsiel_extensions_api_root_namespace

  deps = [ "//extensions/common" ]

  visibility = [ ":api" ]
}

generated_json_strings("generated_api_json_strings") {
  sources = adrbrowsiel_extensions_api_schema_sources +
            adrbrowsiel_extensions_api_uncompiled_sources

  configs = [ "//build/config:precompiled_headers" ]
  bundle_name = "adrbrowsiel"
  schema_include_rules = adrbrowsiel_extensions_api_schema_include_rules

  root_namespace = adrbrowsiel_extensions_api_root_namespace
  deps = adrbrowsiel_extensions_api_deps

  visibility = [ ":api" ]
}

generated_types("generated_api_types") {
  sources = adrbrowsiel_extensions_api_schema_sources
  configs = [ "//build/config:precompiled_headers" ]
  schema_include_rules = adrbrowsiel_extensions_api_schema_include_rules

  root_namespace = adrbrowsiel_extensions_api_root_namespace
  deps = adrbrowsiel_extensions_api_deps
  visibility = [ ":api" ]
}

# Public Targets

group("api") {
  public_deps = [
    ":generated_api_json_strings",
    ":generated_api_registration",
    ":generated_api_types",
  ]
}
