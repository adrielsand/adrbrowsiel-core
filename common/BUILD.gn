import("//build/util/branding.gni")
import("//extensions/buildflags/buildflags.gni")
import("//mojo/public/tools/bindings/mojom.gni")

adrbrowsiel_common_visibility_targets = [
  ":*",
  "//adrbrowsiel:browser_dependencies",
  "//adrbrowsiel:child_dependencies",
  "//adrbrowsiel/browser/*",
  "//adrbrowsiel/renderer/*",
  "//adrbrowsiel/test/*",
  "//adrbrowsiel/utility/*",
  "//chrome/common/*",
  "//chrome/browser/*",
  "//chrome/renderer/*",
  "//chrome/utility/*",
]

source_set("channel_info") {
  visibility = adrbrowsiel_common_visibility_targets

  if (is_linux) {
    sources = [
      "adrbrowsiel_channel_info_posix.cc",
      "adrbrowsiel_channel_info_posix.h",
    ]

    deps = [
      "//base:base",
      "//base:logging_buildflags",
      "//components/version_info:version_info",
    ]

    configs += [ "//adrbrowsiel/build/linux:linux_channel_names" ]
  }
}

source_set("network_constants") {
  visibility = adrbrowsiel_common_visibility_targets +
               # These are layering violation and need to be fixed
               [
                 "//adrbrowsiel/components/content_settings/core/browser",
                 "//adrbrowsiel/components/sync/driver:unit_tests",
                 "//components/sync/driver",
               ]

  sources = [
    "network_constants.cc",
    "network_constants.h",
  ]
}

source_set("pref_names") {
  visibility = adrbrowsiel_common_visibility_targets +
               # These are layering violation and need to be fixed
               [
                 "//adrbrowsiel/components/adrbrowsiel_shields/browser",
                 "//adrbrowsiel/components/content_settings/core/browser",
                 "//adrbrowsiel/components/p3a",
                 "//adrbrowsiel/components/binance/browser",
                 "//adrbrowsiel/ios/app",
                 "//components/omnibox/browser",
               ]

  sources = [
    "pref_names.cc",
    "pref_names.h",
  ]

  deps = [ "//components/gcm_driver:gcm_buildflags" ]
}

source_set("switches") {
  visibility = adrbrowsiel_common_visibility_targets

  sources = [
    "adrbrowsiel_switches.cc",
    "adrbrowsiel_switches.h",
  ]

  deps = [ "//base" ]
}

source_set("common") {
  visibility =
      adrbrowsiel_common_visibility_targets +
      # These are layering violation and need to be fixed
      [
        "//adrbrowsiel/vendor/bat-native-ads:ads",
        "//adrbrowsiel/components/p3a",
        "//adrbrowsiel/components/adrbrowsiel_webtorrent/browser",
        "//adrbrowsiel/components/adrbrowsiel_referrals/browser",
        "//adrbrowsiel/components/adrbrowsiel_ads/browser",
        "//adrbrowsiel/components/adrbrowsiel_wallet/renderer",
        "//adrbrowsiel/components/content_settings/renderer",
        "//adrbrowsiel/components/external_intents/android",
        "//adrbrowsiel/components/webcompat_reporter/browser",
        "//adrbrowsiel/chromium_src/third_party/blink/renderer/modules:browser_tests",
      ]
  if (!is_android && !is_ios) {
    visibility += [ "//adrbrowsiel/components/ipfs/test:adrbrowsiel_ipfs_browser_tests" ]
  }

  # Remove when https://github.com/adrbrowsiel/adrbrowsiel-browser/issues/10653 is resolved
  check_includes = false
  deps = []

  sources = [
    "adrbrowsiel_channel_info.cc",
    "adrbrowsiel_channel_info.h",
    "adrbrowsiel_constants.cc",
    "adrbrowsiel_constants.h",
    "adrbrowsiel_features.cc",
    "adrbrowsiel_features.h",
    "adrbrowsiel_paths.cc",
    "adrbrowsiel_paths.h",
    "resource_bundle_helper.cc",
    "resource_bundle_helper.h",
    "translate_network_constants.cc",
    "translate_network_constants.h",
    "url_constants.cc",
    "url_constants.h",
    "webui_url_constants.cc",
    "webui_url_constants.h",
  ]

  if (!is_android && !is_ios) {
    deps += [ "//adrbrowsiel/common/importer" ]
  }

  if (enable_extensions) {
    sources += [
      "extensions/adrbrowsiel_extensions_api_provider.cc",
      "extensions/adrbrowsiel_extensions_api_provider.h",
      "extensions/whitelist.cc",
      "extensions/whitelist.h",
    ]

    deps += [ "//extensions/common:common_constants" ]

    public_deps = [ "extensions/api" ]
  }

  deps += [
    ":mojo_bindings",
    ":network_constants",
    ":pref_names",
    ":switches",
    "//base",
    "//adrbrowsiel/chromium_src:common",
    "//adrbrowsiel/components/resources",
    "//components/resources",
    "//extensions/buildflags",
    "//services/service_manager",
    "//ui/base",
  ]

  if (!is_ios) {
    sources += [
      "adrbrowsiel_content_client.cc",
      "adrbrowsiel_content_client.h",
    ]

    deps += [
      "//adrbrowsiel/components/resources",
      "//components/resources",
      "//content/public/common",
      "//third_party/widevine/cdm:buildflags",
    ]
  }
}

config("constants_configs") {
  visibility = adrbrowsiel_common_visibility_targets +
               # These are layering violation and need to be fixed
               [ "//chrome/tools/build/mac:infoplist_strings_util" ]
  defines = []
  if (is_mac) {
    defines += [ "adrbrowsiel_PRODUCT_STRING=\"$chrome_product_full_name\"" ]
  }
}

mojom("mojo_bindings") {
  sources = [ "adrbrowsiel_renderer_configuration.mojom" ]

  deps = [ "//mojo/public/mojom/base" ]
}
