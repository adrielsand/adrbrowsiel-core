import("//build/util/branding.gni")
import("//adrbrowsiel/build/config.gni")
import("//media/cdm/library_cdm/cdm_paths.gni")

action("generate_breakpad_symbols") {
  symbols_dir = "$adrbrowsiel_dist_dir/$adrbrowsiel_project_name.breakpad.syms"
  outputs = [
    symbols_dir
  ]

  # On both Windows x86 and x64 we will single thread
  # symbol generation due to Python's Global Interpreter
  # Lock (GIL) which causes a large slowdown if
  # multi-threaded.
  platform_threads = "1"

  args = [
    "--symbols-dir=" + rebase_path(symbols_dir),
    "--jobs=" + platform_threads,
    "--build-dir=" + rebase_path(root_out_dir),
    "--verbose",
    "--clear",
    rebase_path(root_out_dir)
  ]

  script = "//adrbrowsiel/tools/win/generate_breakpad_symbols.py"

  deps = [
    "//third_party/breakpad:dump_syms",
    "//adrbrowsiel/build/win:copy_pdb",
  ]
}

group("symbol_dist_resources") {
  public_deps = [ ":generate_breakpad_symbols" ]
}

copy("dist_resources") {
  sources = [
    "$root_out_dir/chrome_elf.dll",
  ]

  outputs = [
    "$adrbrowsiel_dist_dir/{{source_file_part}}",
  ]

  public_deps = [
    "//chrome/chrome_elf",
    "//third_party/angle:copy_compiler_dll",
  ]
}
