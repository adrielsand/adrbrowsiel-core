import("//adrbrowsiel/build/features.gni")
import("//build/util/branding.gni")

declare_args() {
  # Release channel support.
  # "beta" for beta release.
  # "dev" for dev channel release.
  # "nightly" for nightly channel release.
  # "" for stable channel release.
  adrbrowsiel_channel = ""
  is_release_channel = true
  base_sparkle_update_url = ""
  enable_sparkle = is_official_build && is_mac

  sparkle_dsa_private_key_file = ""
  sparkle_eddsa_private_key = ""
  sparkle_eddsa_public_key = ""

  adrbrowsiel_product_name = ""
  adrbrowsiel_project_name = ""
  adrbrowsiel_exe = ""
  adrbrowsiel_version_major = ""
  adrbrowsiel_version_minor = ""
  adrbrowsiel_version_build = ""
  adrbrowsiel_version_patch = 0
  chrome_version_string = ""
  chrome_version_major = ""
  build_omaha = false
  tag_ap = ""
  target_android_base = ""
  target_android_output_format = ""
  adrbrowsiel_android_keystore_path = "."
  adrbrowsiel_android_keystore_name = ""
  adrbrowsiel_android_keystore_password = ""
  adrbrowsiel_android_key_password = ""

  skip_signing = false

  # Used for generating delta installer on macOS/Windows
  build_delta_installer = false
  last_chrome_installer = ""
}

adrbrowsiel_version = "$adrbrowsiel_version_major.$adrbrowsiel_version_minor.$adrbrowsiel_version_build"

if (adrbrowsiel_exe == "") {
  adrbrowsiel_exe = adrbrowsiel_project_name
}

adrbrowsiel_dist_dir = "$root_out_dir/dist"
if (is_win) {
  adrbrowsiel_exe = "$adrbrowsiel_exe.exe"
  adrbrowsiel_underline_full_version =
      "_$chrome_version_major" + "_$adrbrowsiel_version_major" +
      "_$adrbrowsiel_version_minor" + "_$adrbrowsiel_version_build"
  _channel = ""
  adrbrowsiel_app_guid = "{AFE6A462-C574-4B8A-AF43-4CC60DF4563B}"
  if (is_official_build) {
    if (adrbrowsiel_channel == "beta") {
      _channel = "Beta"
      adrbrowsiel_app_guid = "{103BD053-949B-43A8-9120-2E424887DE11}"
    } else if (adrbrowsiel_channel == "dev") {
      _channel = "Dev"
      adrbrowsiel_app_guid = "{CB2150F2-595F-4633-891A-E39720CE0531}"
    } else if (adrbrowsiel_channel == "nightly") {
      _channel = "Nightly"
      adrbrowsiel_app_guid = "{C6CB981E-DB30-4876-8639-109F8933582C}"
    } else {
      assert(adrbrowsiel_channel == "", "Unknown channel name")
    }
  } else {
    _channel = "Development"
  }
  _arch = ""
  if (target_cpu == "x86") {
    _arch = "32"
  }
  adrbrowsiel_installer_exe = "adrbrowsiel_installer$adrbrowsiel_underline_full_version.exe"
  adrbrowsiel_stub_installer_exe =
      "adrbrowsielBrowser$_channel" + "Setup$_arch$adrbrowsiel_underline_full_version.exe"
  adrbrowsiel_untagged_stub_installer_exe =
      "adrbrowsielBrowserUntagged$_channel" +
      "Setup$_arch$adrbrowsiel_underline_full_version.exe"
  adrbrowsiel_standalone_installer_exe =
      "adrbrowsielBrowserStandalone$_channel" +
      "Setup$_arch$adrbrowsiel_underline_full_version.exe"
  adrbrowsiel_silent_installer_exe = "adrbrowsielBrowserStandaloneSilent$_channel" +
                               "Setup$_arch$adrbrowsiel_underline_full_version.exe"
  adrbrowsiel_untagged_installer_exe = "adrbrowsielBrowserStandaloneUntagged$_channel" +
                                 "Setup$_arch$adrbrowsiel_underline_full_version.exe"
} else if (is_mac) {
  adrbrowsiel_exe = "$chrome_product_full_name.app"
  adrbrowsiel_dmg = "$chrome_product_full_name.dmg"
  adrbrowsiel_pkg = "$chrome_product_full_name.pkg"
  adrbrowsiel_delta = "$chrome_product_full_name.delta"

  adrbrowsiel_product_dir_name_suffix = ""
  if (is_official_build) {
    if (adrbrowsiel_channel == "beta") {
      adrbrowsiel_product_dir_name_suffix = "-Beta"
    } else if (adrbrowsiel_channel == "dev") {
      adrbrowsiel_product_dir_name_suffix = "-Dev"
    } else if (adrbrowsiel_channel == "nightly") {
      adrbrowsiel_product_dir_name_suffix = "-Nightly"
    } else {
      assert(adrbrowsiel_channel == "", "Unknown channel name")
    }
  } else {
    adrbrowsiel_product_dir_name_suffix = "-Development"
  }
  adrbrowsiel_product_dir_name =
      "adrbrowsielSoftware/adrbrowsiel-Browser$adrbrowsiel_product_dir_name_suffix"

  if (base_sparkle_update_url == "") {
    base_sparkle_update_url =
        "https://updates.adrbrowsielsoftware.com/sparkle/adrbrowsiel-Browser"
  }
  _update_channel = adrbrowsiel_channel
  if (_update_channel == "") {
    _update_channel = "stable"
  }
  if (target_cpu == "arm64") {
    _update_channel += "-arm64"
  }
  adrbrowsiel_feed_url = "$base_sparkle_update_url/$_update_channel/appcast.xml"
}

adrbrowsiel_platform = "darwin"
if (is_win) {
  adrbrowsiel_platform = "win32"
} else if (is_linux) {
  adrbrowsiel_platform = "linux"
}

is_release_channel = adrbrowsiel_channel == ""

adrbrowsiel_android_output = ""
if (is_android) {
  adrbrowsiel_android_output += "$root_out_dir/apks/adrbrowsiel"
  if (target_android_base == "modern") {
    assert(target_cpu != "arm64" && target_cpu != "x64")
    adrbrowsiel_android_output += "Modern"
  } else if (target_cpu == "arm64" || target_cpu == "x64" ||
             target_android_base == "mono") {
    adrbrowsiel_android_output += "Mono"
  }

  if (target_android_output_format == "aab") {
    adrbrowsiel_android_output += "$target_cpu.aab"
  } else {
    adrbrowsiel_android_output += "$target_cpu.apk"
  }
}
