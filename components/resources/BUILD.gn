import(
    "//adrbrowsiel/components/adrbrowsiel_perf_predictor/browser/buildflags/buildflags.gni")
import("//adrbrowsiel/components/adrbrowsiel_rewards/browser/buildflags/buildflags.gni")
import("//adrbrowsiel/components/adrbrowsiel_wallet/common/buildflags/buildflags.gni")
import("//adrbrowsiel/components/crypto_dot_com/browser/buildflags/buildflags.gni")
import("//adrbrowsiel/components/decentralized_dns/buildflags/buildflags.gni")
import("//adrbrowsiel/components/ftx/browser/buildflags/buildflags.gni")
import("//adrbrowsiel/components/ipfs/buildflags/buildflags.gni")
import("//adrbrowsiel/components/sidebar/buildflags/buildflags.gni")
import("//adrbrowsiel/components/speedreader/buildflags.gni")
import("//adrbrowsiel/components/tor/buildflags/buildflags.gni")
import("//build/config/locales.gni")
import("//extensions/buildflags/buildflags.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/repack.gni")

grit("static_resources") {
  source = "adrbrowsiel_components_resources.grd"

  output_name = "adrbrowsiel_components_resources_new"

  outputs = [
    "grit/adrbrowsiel_components_resources.h",
    "adrbrowsiel_components_static.pak",
  ]
  deps = [ ":strings" ]

  if (enable_extensions) {
    deps += [ "//adrbrowsiel/components/adrbrowsiel_extension/extension/adrbrowsiel_extension" ]
  }
  if (adrbrowsiel_rewards_enabled) {
    deps += [ "//adrbrowsiel/components/adrbrowsiel_rewards/resources" ]
    if (enable_extensions) {
      deps +=
          [ "//adrbrowsiel/components/adrbrowsiel_rewards/resources/extension:resources" ]
    }
  }
  if (enable_tor) {
    deps += [ "//adrbrowsiel/components/tor/resources" ]
  }

  defines = [
    "enable_adrbrowsiel_perf_predictor=$enable_adrbrowsiel_perf_predictor",
    "enable_speedreader=$enable_speedreader",
    "ipfs_enabled=$ipfs_enabled",
    "crypto_dot_com_enabled=$crypto_dot_com_enabled",
    "decentralized_dns_enabled=$decentralized_dns_enabled",
    "enable_ftx=$enable_ftx",
  ]

  grit_flags = [
    "-E",
    "root_gen_dir=" + rebase_path(root_gen_dir, root_build_dir),
  ]

  output_dir = "$root_gen_dir/components"
  resource_ids = "//adrbrowsiel/browser/resources/resource_ids"
}

repack("resources") {
  deps = [
    ":static_resources",
    "//adrbrowsiel/components/adrbrowsiel_adblock_ui:generated_resources",
    "//adrbrowsiel/components/cosmetic_filters/resources/data:generated_resources",
  ]

  sources = [
    "$root_gen_dir/adrbrowsiel/components/adrbrowsiel_adblock/resources/adrbrowsiel_adblock_generated.pak",
    "$root_gen_dir/adrbrowsiel/components/cosmetic_filters/resources/cosmetic_filters_generated.pak",
    "$root_gen_dir/components/adrbrowsiel_components_static.pak",
  ]

  if (!is_android) {
    deps += [
      "//adrbrowsiel/components/adrbrowsiel_new_tab_ui:generated_resources",
      "//adrbrowsiel/components/adrbrowsiel_welcome_ui:generated_resources",
      "//adrbrowsiel/components/webcompat_reporter/ui:generated_resources",
    ]

    sources += [
      "$root_gen_dir/adrbrowsiel/components/adrbrowsiel_new_tab/resources/adrbrowsiel_new_tab_generated.pak",
      "$root_gen_dir/adrbrowsiel/components/adrbrowsiel_welcome/resources/adrbrowsiel_welcome_generated.pak",
      "$root_gen_dir/adrbrowsiel/components/webcompat_reporter/resources/webcompat_reporter_generated.pak",
    ]
  }

  if (ipfs_enabled) {
    deps += [ "//adrbrowsiel/components/ipfs_ui:generated_resources" ]
    sources += [
      "$root_gen_dir/adrbrowsiel/components/ipfs_ui/resources/ipfs_generated.pak",
    ]
  }

  if (adrbrowsiel_wallet_enabled) {
    if (!is_android) {
      deps += [ "//adrbrowsiel/components/adrbrowsiel_wallet_ui:resources" ]
      sources += [ "$root_gen_dir/adrbrowsiel/components/adrbrowsiel_wallet/resources/adrbrowsiel_wallet.pak" ]
    }

    deps += [ "//adrbrowsiel/components/adrbrowsiel_wallet/resources:ethereum_provider_generated_resources" ]
    sources += [ "$root_gen_dir/adrbrowsiel/components/adrbrowsiel_wallet/resources/adrbrowsiel_wallet_script_generated.pak" ]
  }

  output = "$root_gen_dir/components/adrbrowsiel_components_resources.pak"
}

grit("strings") {
  source = "adrbrowsiel_components_strings.grd"

  outputs = [ "grit/adrbrowsiel_components_strings.h" ]

  defines = [
    "enable_speedreader=$enable_speedreader",
    "ipfs_enabled=$ipfs_enabled",
    "adrbrowsiel_wallet_enabled=$adrbrowsiel_wallet_enabled",
    "decentralized_dns_enabled=$decentralized_dns_enabled",
    "crypto_dot_com_enabled=$crypto_dot_com_enabled",
    "enable_tor=$enable_tor",
    "enable_sidebar=$enable_sidebar",
    "enable_ftx=$enable_ftx",
  ]

  foreach(locale, locales_with_pseudolocales) {
    outputs += [ "adrbrowsiel_components_strings_$locale.pak" ]
  }

  output_dir = "$root_gen_dir/components"
  resource_ids = "//adrbrowsiel/browser/resources/resource_ids"
}

action("about_credits") {
  script = "//adrbrowsiel/script/check_npm_licenses.py"

  inputs = [ "//adrbrowsiel/package.json" ]

  outputs = [ "$root_gen_dir/npm_licenses.checked" ]

  args = [ rebase_path("$root_gen_dir/npm_licenses.checked") ]
}
