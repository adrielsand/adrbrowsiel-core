import("//adrbrowsiel/components/common/typescript.gni")
import("//build/config/locales.gni")
import("//extensions/buildflags/buildflags.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/repack.gni")

assert(enable_extensions)

pack_web_resources("extension_generated_resources") {
  resource_name = "adrbrowsiel_rewards_panel"
  output_dir = "$root_gen_dir/adrbrowsiel/components/adrbrowsiel_rewards/resources/extension"
  deps = [
    "adrbrowsiel_rewards:adrbrowsiel_rewards_panel"
  ]
}

grit("static_resources") {
  source = "extension_static_resources.grd"
  outputs = [
    "grit/adrbrowsiel_rewards_extension_resources_map.cc",
    "grit/adrbrowsiel_rewards_extension_resources_map.h",
    "grit/adrbrowsiel_rewards_extension_resources.h",
    "adrbrowsiel_rewards_extension_static.pak",
  ]

  deps = [
  ]

  resource_ids = "//adrbrowsiel/browser/resources/resource_ids"

  output_dir = "$root_gen_dir/adrbrowsiel/components/adrbrowsiel_rewards/resources/extension"
}

repack("resources") {
  deps = [
    ":extension_generated_resources",
    ":static_resources",
  ]

  sources = [
    "$root_gen_dir/adrbrowsiel/components/adrbrowsiel_rewards/resources/extension/adrbrowsiel_rewards_extension_static.pak",
    "$root_gen_dir/adrbrowsiel/components/adrbrowsiel_rewards/resources/extension/adrbrowsiel_rewards_panel_generated.pak",
  ]

  output = "$root_gen_dir/adrbrowsiel/components/adrbrowsiel_rewards/resources/extension/adrbrowsiel_rewards_extension_resources.pak"
}

if (is_mac) {
  group("framework_bundle_data") {
    deps = []
    foreach(locale, locales) {
      deps += [":framework_bundle_data_${locale}"]
    }
  }

  foreach(locale, locales) {
    bundle_data("framework_bundle_data_${locale}") {
      locale = string_replace(locale, "-", "_")
      source_locale = string_replace(locale, "nb", "no")
      sources = [
        "adrbrowsiel_rewards/_locales/$source_locale/messages.json"
      ]
      outputs = [
        "{{bundle_resources_dir}}/adrbrowsiel_rewards/_locales/$locale/{{source_file_part}}"
      ]
    }
  }
} else {
  group("locales") {
    deps = []
    foreach(locale, locales) {
      deps += [":locales_${locale}"]
    }
  }
  foreach(locale, locales) {
    copy("locales_${locale}") {
      locale = string_replace(locale, "-", "_")
      source_locale = string_replace(locale, "nb", "no")
      sources = [
        "adrbrowsiel_rewards/_locales/${source_locale}/messages.json",
      ]
      outputs = [
        "$root_out_dir/resources/adrbrowsiel_rewards/_locales/${locale}/{{source_file_part}}",
      ]
    }
  }
}
