# Copyright (c) 2020 The adrbrowsiel Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# you can obtain one at http://mozilla.org/MPL/2.0/.

import("//adrbrowsiel/build/config.gni")
import("//adrbrowsiel/components/adrbrowsiel_wayback_machine/buildflags/buildflags.gni")
import("//adrbrowsiel/components/tor/buildflags/buildflags.gni")
import("//chrome/browser/resources/tools/optimize_webui.gni")
import("//chrome/common/features.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")
import("//tools/polymer/polymer.gni")
import("//ui/webui/resources/tools/generate_grd.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")

preprocess_folder = "preprocessed"
preprocess_manifest = "adrbrowsiel_preprocessed_manifest.json"
preprocess_gen_manifest = "adrbrowsiel_preprocessed_gen_manifest.json"

grit("resources") {
  grit_flags = [
    "-E",
    "root_gen_dir=" + rebase_path(root_gen_dir, root_build_dir),
    "-E",
    "root_src_dir=" + rebase_path("//", root_build_dir),
  ]

  defines = chrome_grit_defines
  defines += [
    "is_release_channel=$is_release_channel",
    "enable_tor=$enable_tor",
    "enable_adrbrowsiel_wayback_machine=$enable_adrbrowsiel_wayback_machine",
  ]

  enable_input_discovery_for_gn_analyze = false
  defines +=
      [ "SHARED_INTERMEDIATE_DIR=" + rebase_path(root_gen_dir, root_build_dir) ]
  settings_gen_dir = "$root_gen_dir/adrbrowsiel/browser/resources/settings"
  source = "$settings_gen_dir/adrbrowsiel_settings_resources.grd"
  deps = [ ":build_grd" ]

  outputs = [
    "grit/adrbrowsiel_settings_resources.h",
    "grit/adrbrowsiel_settings_resources_map.cc",
    "grit/adrbrowsiel_settings_resources_map.h",
    "adrbrowsiel_settings_resources.pak",
  ]

  output_dir = "$root_gen_dir/adrbrowsiel/browser/resources/settings"
  resource_ids = "//adrbrowsiel/browser/resources/resource_ids"
}

generate_grd("build_grd") {
  grd_prefix = "adrbrowsiel_settings"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
  input_files = [
    "adrbrowsiel_sync_page/device_computer.svg",
    "adrbrowsiel_sync_page/device_mobile.svg",
    "adrbrowsiel_sync_page/qr_scanning.svg",
    "adrbrowsiel_sync_page/start_icon.svg",
    "images/cookies_banner.svg",
    "images/cookies_banner_dark.svg",
    "images/permissions_banner.svg",
    "images/permissions_banner_dark.svg",
    "images/safe_browsing_banner.svg",
    "images/safe_browsing_banner_dark.svg",
  ]
  input_files_base_dir = rebase_path(".", "//")

  deps = [
    ":preprocess",
    ":preprocess_generated",
  ]
  manifest_files = [
    "$root_gen_dir/chrome/browser/resources/settings/$preprocess_manifest",
    "$root_gen_dir/chrome/browser/resources/settings/$preprocess_gen_manifest",
  ]
}

preprocess_if_expr("preprocess") {
  in_folder = "./"
  out_folder =
      "$root_gen_dir/chrome/browser/resources/settings/$preprocess_folder"
  out_manifest =
      "$root_gen_dir/chrome/browser/resources/settings/$preprocess_manifest"
  in_files = [
    "adrbrowsiel_appearance_page/adrbrowsiel_appearance_browser_proxy.js",
    "adrbrowsiel_clear_browsing_data_dialog/adrbrowsiel_clear_browsing_data_dialog_behavior.js",
    "adrbrowsiel_overrides/about_page.js",
    "adrbrowsiel_overrides/appearance_page.js",
    "adrbrowsiel_overrides/basic_page.js",
    "adrbrowsiel_overrides/clear_browsing_data_dialog.js",
    "adrbrowsiel_overrides/cookies_page.js",
    "adrbrowsiel_overrides/default_browser_page.js",
    "adrbrowsiel_overrides/icons.js",
    "adrbrowsiel_overrides/import_data_dialog.js",
    "adrbrowsiel_overrides/index.js",
    "adrbrowsiel_overrides/page_visibility.js",
    "adrbrowsiel_overrides/passwords_section.js",
    "adrbrowsiel_overrides/payments_section.js",
    "adrbrowsiel_overrides/people_page.js",
    "adrbrowsiel_overrides/personalization_options.js",
    "adrbrowsiel_overrides/printing_page.js",
    "adrbrowsiel_overrides/reset_profile_dialog.js",
    "adrbrowsiel_overrides/safety_check_passwords_child.js",
    "adrbrowsiel_overrides/security_page.js",
    "adrbrowsiel_overrides/settings_menu.js",
    "adrbrowsiel_overrides/settings_section.js",
    "adrbrowsiel_overrides/settings_ui.js",
    "adrbrowsiel_overrides/site_settings_page.js",
    "adrbrowsiel_overrides/sync_account_control.js",
    "adrbrowsiel_overrides/sync_controls.js",
    "adrbrowsiel_sync_page/adrbrowsiel_sync_browser_proxy.js",
  ]
}

preprocess_if_expr("preprocess_generated") {
  deps = [ ":web_modules" ]
  defines = [
    "enable_tor=$enable_tor",
    "enable_adrbrowsiel_wayback_machine=$enable_adrbrowsiel_wayback_machine",
  ]
  in_folder = target_gen_dir
  out_folder =
      "$root_gen_dir/chrome/browser/resources/settings/$preprocess_folder"
  out_manifest =
      "$root_gen_dir/chrome/browser/resources/settings/$preprocess_gen_manifest"
  in_files = [
    "adrbrowsiel_appearance_page/adrbrowsiel_theme.js",
    "adrbrowsiel_appearance_page/sidebar.js",
    "adrbrowsiel_appearance_page/super_referral.js",
    "adrbrowsiel_appearance_page/toolbar.js",
    "adrbrowsiel_clear_browsing_data_dialog/adrbrowsiel_clear_browsing_data_on_exit_page.js",
    "adrbrowsiel_ipfs_page/adrbrowsiel_ipfs_browser_proxy.m.js",
    "adrbrowsiel_ipfs_page/adrbrowsiel_ipfs_page.m.js",
    "adrbrowsiel_ipfs_page/change_ipfs_gateway_dialog.js",
    "adrbrowsiel_ipfs_page/add_p2p_key_dialog.js",
    "adrbrowsiel_ipfs_page/p2p_keys_subpage.m.js",
    "adrbrowsiel_default_extensions_page/adrbrowsiel_default_extensions_browser_proxy.m.js",
    "adrbrowsiel_default_extensions_page/adrbrowsiel_default_extensions_page.m.js",
    "adrbrowsiel_help_tips_page/adrbrowsiel_help_tips_page.m.js",
    "adrbrowsiel_icons.m.js",
    "adrbrowsiel_new_tab_page/adrbrowsiel_new_tab_browser_proxy.m.js",
    "adrbrowsiel_new_tab_page/adrbrowsiel_new_tab_page.m.js",
    "adrbrowsiel_privacy_page/adrbrowsiel_personalization_options.m.js",
    "adrbrowsiel_privacy_page/adrbrowsiel_privacy_page_browser_proxy.m.js",
    "adrbrowsiel_sync_page/adrbrowsiel_sync_code_dialog.js",
    "adrbrowsiel_sync_page/adrbrowsiel_sync_configure.js",
    "adrbrowsiel_sync_page/adrbrowsiel_sync_page.js",
    "adrbrowsiel_sync_page/adrbrowsiel_sync_setup.js",
    "adrbrowsiel_sync_page/adrbrowsiel_sync_subpage.js",
    "adrbrowsiel_wallet_page/adrbrowsiel_wallet_browser_proxy.m.js",
    "adrbrowsiel_wallet_page/adrbrowsiel_wallet_page.m.js",
    "default_adrbrowsiel_shields_page/default_adrbrowsiel_shields_browser_proxy.m.js",
    "default_adrbrowsiel_shields_page/default_adrbrowsiel_shields_page.m.js",
    "getting_started_page/getting_started.js",
    "social_blocking_page/social_blocking_page.m.js",
  ]
}

group("web_modules") {
  public_deps = [
    # local targets
    ":icons_module",

    # Sub-folder targets
    "adrbrowsiel_appearance_page:web_modules",
    "adrbrowsiel_clear_browsing_data_dialog:web_modules",
    "adrbrowsiel_default_extensions_page:web_modules",
    "adrbrowsiel_help_tips_page:web_modules",
    "adrbrowsiel_ipfs_page:web_modules",
    "adrbrowsiel_new_tab_page:web_modules",
    "adrbrowsiel_privacy_page:web_modules",
    "adrbrowsiel_sync_page:web_modules",
    "adrbrowsiel_wallet_page:web_modules",
    "default_adrbrowsiel_shields_page:web_modules",
    "getting_started_page:web_modules",
    "social_blocking_page:web_modules",
  ]
}

polymer_modulizer("icons") {
  js_file = "adrbrowsiel_icons.m.js"
  html_file = "adrbrowsiel_icons.html"
  html_type = "iron-iconset"
}
