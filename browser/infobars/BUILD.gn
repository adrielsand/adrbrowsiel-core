import("//adrbrowsiel/components/adrbrowsiel_sync/buildflags/buildflags.gni")
import("//adrbrowsiel/components/adrbrowsiel_wallet/common/buildflags/buildflags.gni")
import("//adrbrowsiel/components/adrbrowsiel_wayback_machine/buildflags/buildflags.gni")
import("//adrbrowsiel/components/ipfs/buildflags/buildflags.gni")
import("//build/config/features.gni")

source_set("infobars") {
  # Remove when https://github.com/adrbrowsiel/adrbrowsiel-browser/issues/10649 is resolved
  check_includes = false
  sources = [
    "adrbrowsiel_confirm_p3a_infobar_delegate.cc",
    "adrbrowsiel_confirm_p3a_infobar_delegate.h",
  ]

  deps = [
    "//base",
    "//adrbrowsiel/app:adrbrowsiel_generated_resources_grit",
    "//adrbrowsiel/browser/ui",
    "//adrbrowsiel/components/p3a",
    "//chrome/app:chromium_strings",
    "//chrome/browser/ui",
    "//components/infobars/core",
    "//components/prefs",
    "//components/strings",
    "//ui/base",
    "//ui/views",
    "//url",
  ]

  if (adrbrowsiel_wallet_enabled) {
    sources += [
      "crypto_wallets_infobar_delegate.cc",
      "crypto_wallets_infobar_delegate.h",
    ]

    deps += [
      "//adrbrowsiel/browser/adrbrowsiel_wallet",
      "//adrbrowsiel/common:pref_names",
      "//adrbrowsiel/components/adrbrowsiel_wallet/browser",
      "//adrbrowsiel/components/adrbrowsiel_wallet/common",
      "//components/user_prefs:user_prefs",
      "//extensions/common:common_constants",
    ]
  }

  if (ipfs_enabled) {
    deps += [
      "//adrbrowsiel/components/ipfs",
      "//components/user_prefs",
    ]
  }

  if (enable_adrbrowsiel_wayback_machine) {
    sources += [
      "adrbrowsiel_wayback_machine_delegate_impl.cc",
      "adrbrowsiel_wayback_machine_delegate_impl.h",
    ]

    deps += [
      "//adrbrowsiel/common:switches",
      "//adrbrowsiel/components/adrbrowsiel_wayback_machine",
    ]
  }

  if (enable_adrbrowsiel_sync) {
    sources += [
      "sync_v2_migrate_infobar_delegate.cc",
      "sync_v2_migrate_infobar_delegate.h",
    ]

    deps += [
      "//adrbrowsiel/common:common",
      "//adrbrowsiel/components/adrbrowsiel_sync:features",
      "//adrbrowsiel/components/adrbrowsiel_sync:prefs",
    ]
  }
}
