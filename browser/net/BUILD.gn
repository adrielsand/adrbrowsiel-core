import("//adrbrowsiel/browser/translate/buildflags/buildflags.gni")
import("//adrbrowsiel/components/adrbrowsiel_referrals/buildflags/buildflags.gni")
import("//adrbrowsiel/components/adrbrowsiel_wallet/common/buildflags/buildflags.gni")
import("//adrbrowsiel/components/adrbrowsiel_webtorrent/browser/buildflags/buildflags.gni")
import("//adrbrowsiel/components/decentralized_dns/buildflags/buildflags.gni")
import("//adrbrowsiel/components/ipfs/buildflags/buildflags.gni")
import("//build/config/features.gni")

# Refer to the keyed API spec for more details about the adrbrowsiel Services Key
defines = adrbrowsiel_service_key_defines

source_set("net") {
  # Remove when https://github.com/adrielsand/adrbrowsiel-browser/issues/10659 is resolved
  check_includes = false
  configs += [ "//adrbrowsiel/build/geolocation" ]
  sources = [
    "adrbrowsiel_ad_block_csp_network_delegate_helper.cc",
    "adrbrowsiel_ad_block_csp_network_delegate_helper.h",
    "adrbrowsiel_ad_block_tp_network_delegate_helper.cc",
    "adrbrowsiel_ad_block_tp_network_delegate_helper.h",
    "adrbrowsiel_block_safebrowsing_urls.cc",
    "adrbrowsiel_block_safebrowsing_urls.h",
    "adrbrowsiel_common_static_redirect_network_delegate_helper.cc",
    "adrbrowsiel_common_static_redirect_network_delegate_helper.h",
    "adrbrowsiel_httpse_network_delegate_helper.cc",
    "adrbrowsiel_httpse_network_delegate_helper.h",
    "adrbrowsiel_proxying_url_loader_factory.cc",
    "adrbrowsiel_proxying_url_loader_factory.h",
    "adrbrowsiel_proxying_web_socket.cc",
    "adrbrowsiel_proxying_web_socket.h",
    "adrbrowsiel_request_handler.cc",
    "adrbrowsiel_request_handler.h",
    "adrbrowsiel_site_hacks_network_delegate_helper.cc",
    "adrbrowsiel_site_hacks_network_delegate_helper.h",
    "adrbrowsiel_static_redirect_network_delegate_helper.cc",
    "adrbrowsiel_static_redirect_network_delegate_helper.h",
    "adrbrowsiel_stp_util.cc",
    "adrbrowsiel_stp_util.h",
    "adrbrowsiel_system_request_handler.cc",
    "adrbrowsiel_system_request_handler.h",
    "global_privacy_control_network_delegate_helper.cc",
    "global_privacy_control_network_delegate_helper.h",
    "resource_context_data.cc",
    "resource_context_data.h",
    "url_context.cc",
    "url_context.h",
  ]

  deps = [
    "//base",
    "//adrbrowsiel/app:adrbrowsiel_generated_resources_grit",
    "//adrbrowsiel/browser/safebrowsing",
    "//adrbrowsiel/browser/translate/buildflags",
    "//adrbrowsiel/common",
    "//adrbrowsiel/components/adrbrowsiel_component_updater/browser",
    "//adrbrowsiel/components/adrbrowsiel_referrals/buildflags",
    "//adrbrowsiel/components/adrbrowsiel_shields/browser",
    "//adrbrowsiel/components/adrbrowsiel_shields/common",
    "//adrbrowsiel/components/adrbrowsiel_wallet/common/buildflags",
    "//adrbrowsiel/components/adrbrowsiel_webtorrent/browser/buildflags",
    "//adrbrowsiel/components/decentralized_dns/buildflags",
    "//adrbrowsiel/components/ipfs/buildflags",
    "//adrbrowsiel/extensions:common",
    "//components/content_settings/core/browser",
    "//components/prefs",
    "//components/user_prefs",
    "//content/public/browser",
    "//content/public/common",
    "//extensions/common:common_constants",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
    "//net",
    "//services/network:network_service",
    "//services/network/public/cpp",
    "//services/network/public/mojom",
    "//third_party/blink/public/common",
    "//third_party/blink/public/mojom:mojom_platform_headers",
    "//third_party/re2",
    "//url",
  ]

  if (ipfs_enabled) {
    sources += [
      "ipfs_redirect_network_delegate_helper.cc",
      "ipfs_redirect_network_delegate_helper.h",
    ]
    deps += [
      "//adrbrowsiel/components/ipfs",
      "//chrome/common:channel_info",
    ]
  }

  if (enable_adrbrowsiel_referrals) {
    sources += [
      "adrbrowsiel_referrals_network_delegate_helper.cc",
      "adrbrowsiel_referrals_network_delegate_helper.h",
    ]

    deps += [ "//adrbrowsiel/components/adrbrowsiel_referrals/browser" ]
  }

  if (enable_adrbrowsiel_webtorrent) {
    sources += [
      "adrbrowsiel_torrent_redirect_network_delegate_helper.cc",
      "adrbrowsiel_torrent_redirect_network_delegate_helper.h",
    ]

    deps += [
      "//adrbrowsiel/components/adrbrowsiel_webtorrent/browser/",
      "//extensions/browser",
    ]
  }

  if (enable_adrbrowsiel_translate_go) {
    sources += [
      "adrbrowsiel_translate_redirect_network_delegate_helper.cc",
      "adrbrowsiel_translate_redirect_network_delegate_helper.h",
    ]
  }

  if (decentralized_dns_enabled && adrbrowsiel_wallet_enabled) {
    sources += [
      "decentralized_dns_network_delegate_helper.cc",
      "decentralized_dns_network_delegate_helper.h",
    ]

    deps += [
      "//adrbrowsiel/components/adrbrowsiel_wallet/browser",
      "//adrbrowsiel/components/decentralized_dns",
    ]
  }
}
