import("//adrbrowsiel/components/binance/browser/buildflags/buildflags.gni")
import("//adrbrowsiel/components/adrbrowsiel_rewards/browser/buildflags/buildflags.gni")
import("//adrbrowsiel/components/adrbrowsiel_together/buildflags/buildflags.gni")
import("//adrbrowsiel/components/adrbrowsiel_wallet/browser/config.gni")
import("//adrbrowsiel/components/adrbrowsiel_wallet/common/buildflags/buildflags.gni")
import("//adrbrowsiel/components/adrbrowsiel_wayback_machine/buildflags/buildflags.gni")
import("//adrbrowsiel/components/adrbrowsiel_webtorrent/browser/buildflags/buildflags.gni")
import("//adrbrowsiel/components/crypto_dot_com/browser/buildflags/buildflags.gni")
import("//adrbrowsiel/components/decentralized_dns/buildflags/buildflags.gni")
import("//adrbrowsiel/components/ftx/browser/buildflags/buildflags.gni")
import("//adrbrowsiel/components/gemini/browser/buildflags/buildflags.gni")
import("//adrbrowsiel/components/ipfs/buildflags/buildflags.gni")
import("//adrbrowsiel/components/sidebar/buildflags/buildflags.gni")
import("//adrbrowsiel/components/tor/buildflags/buildflags.gni")
import("//build/config/features.gni")
import("//components/gcm_driver/config.gni")

source_set("extensions") {
  # Remove when https://github.com/adrbrowsiel/adrbrowsiel-browser/issues/10661 is resolved
  check_includes = false
  sources = [
    "api/adrbrowsiel_action_api.cc",
    "api/adrbrowsiel_action_api.h",
    "api/adrbrowsiel_extensions_api_client.cc",
    "api/adrbrowsiel_extensions_api_client.h",
    "api/adrbrowsiel_shields_api.cc",
    "api/adrbrowsiel_shields_api.h",
    "api/adrbrowsiel_theme_api.cc",
    "api/adrbrowsiel_theme_api.h",
    "api/adrbrowsiel_today_api.cc",
    "api/adrbrowsiel_today_api.h",
    "api/greaselion_api.cc",
    "api/greaselion_api.h",
    "api/settings_private/adrbrowsiel_prefs_util.cc",
    "api/settings_private/adrbrowsiel_prefs_util.h",
    "adrbrowsiel_component_loader.cc",
    "adrbrowsiel_component_loader.h",
    "adrbrowsiel_extension_management.cc",
    "adrbrowsiel_extension_management.h",
    "adrbrowsiel_extension_provider.cc",
    "adrbrowsiel_extension_provider.h",
    "adrbrowsiel_extension_service.cc",
    "adrbrowsiel_extension_service.h",
    "adrbrowsiel_extensions_browser_api_provider.cc",
    "adrbrowsiel_extensions_browser_api_provider.h",
    "adrbrowsiel_extensions_browser_client_impl.cc",
    "adrbrowsiel_extensions_browser_client_impl.h",
    "adrbrowsiel_theme_event_router.cc",
    "adrbrowsiel_theme_event_router.h",
    "updater/adrbrowsiel_update_client_config.cc",
    "updater/adrbrowsiel_update_client_config.h",
  ]

  deps = [
    ":resources",
    "//base",
    "//adrbrowsiel/app:adrbrowsiel_generated_resources_grit",
    "//adrbrowsiel/browser/profiles",
    "//adrbrowsiel/browser/webcompat_reporter",
    "//adrbrowsiel/common",
    "//adrbrowsiel/common:pref_names",
    "//adrbrowsiel/common/extensions/api",
    "//adrbrowsiel/components/adrbrowsiel_component_updater/browser",
    "//adrbrowsiel/components/adrbrowsiel_rewards/browser/buildflags",
    "//adrbrowsiel/components/adrbrowsiel_shields/browser",
    "//adrbrowsiel/components/adrbrowsiel_shields/common",
    "//adrbrowsiel/components/adrbrowsiel_today/browser",
    "//adrbrowsiel/components/adrbrowsiel_wayback_machine:buildflags",
    "//adrbrowsiel/components/decentralized_dns/buildflags",
    "//adrbrowsiel/components/ipfs/buildflags",
    "//adrbrowsiel/components/sidebar/buildflags",
    "//adrbrowsiel/components/tor/buildflags",
    "//chrome/browser/extensions",
    "//chrome/common",
    "//components/gcm_driver:gcm_buildflags",
    "//components/gcm_driver:gcm_driver",
    "//components/omnibox/browser:browser",
    "//components/prefs",
    "//components/services/patch/content",
    "//components/services/unzip/content",
    "//components/update_client:patch_impl",
    "//components/update_client:unzip_impl",
    "//content/public/browser",
    "//extensions/browser",
    "//extensions/common",
    "//third_party/re2",
    "//ui/base",
    "//url",
  ]

  if (enable_sidebar) {
    deps += [ "//adrbrowsiel/components/sidebar" ]
  }

  if (adrbrowsiel_wallet_enabled) {
    sources += [
      "api/adrbrowsiel_wallet_api.cc",
      "api/adrbrowsiel_wallet_api.h",
      "adrbrowsiel_wallet_util.cc",
      "adrbrowsiel_wallet_util.h",
    ]
    configs += [
      ":infura_config",
      ":adrbrowsiel_services_config",
    ]
    deps += [
      "//adrbrowsiel/components/adrbrowsiel_wallet/browser",
      "//adrbrowsiel/components/adrbrowsiel_wallet/common",
    ]
  }

  if (ipfs_enabled) {
    sources += [
      "api/ipfs_api.cc",
      "api/ipfs_api.h",
    ]
    deps += [ "//adrbrowsiel/components/ipfs" ]
  }

  if (decentralized_dns_enabled) {
    deps += [ "//adrbrowsiel/components/decentralized_dns" ]
  }

  if (adrbrowsiel_rewards_enabled) {
    sources += [
      "api/adrbrowsiel_rewards_api.cc",
      "api/adrbrowsiel_rewards_api.h",
      "api/rewards_notifications_api.cc",
      "api/rewards_notifications_api.h",
    ]

    deps += [
      "//adrbrowsiel/browser/adrbrowsiel_ads",
      "//adrbrowsiel/components/adrbrowsiel_ads/browser",
      "//adrbrowsiel/components/adrbrowsiel_rewards/browser",
      "//adrbrowsiel/vendor/bat-native-ads:headers",
      "//adrbrowsiel/vendor/bat-native-ledger:headers",
    ]
  }
  if (enable_adrbrowsiel_webtorrent) {
    deps += [ "//adrbrowsiel/components/adrbrowsiel_webtorrent/browser" ]
    sources += [
      "adrbrowsiel_webtorrent_navigation_throttle.cc",
      "adrbrowsiel_webtorrent_navigation_throttle.h",
    ]
  }
  if (enable_adrbrowsiel_wayback_machine) {
    deps += [ "//adrbrowsiel/components/adrbrowsiel_wayback_machine" ]
  }
  if (gemini_enabled) {
    sources += [
      "api/gemini_api.cc",
      "api/gemini_api.h",
    ]
    deps += [
      "//adrbrowsiel/components/gemini/browser",
      "//adrbrowsiel/components/ntp_widget_utils/browser",
    ]
  }

  if (enable_ftx) {
    sources += [
      "api/ftx_api.cc",
      "api/ftx_api.h",
    ]
    deps += [
      "//adrbrowsiel/components/ftx/browser",
      "//adrbrowsiel/components/ftx/common",
      "//adrbrowsiel/components/ntp_widget_utils/browser",
    ]
  }

  if (binance_enabled) {
    sources += [
      "api/binance_api.cc",
      "api/binance_api.h",
    ]

    deps += [
      "//adrbrowsiel/components/binance/browser",
      "//adrbrowsiel/components/ntp_widget_utils/browser",
    ]
  }

  if (adrbrowsiel_together_enabled) {
    sources += [
      "api/adrbrowsiel_together_api.cc",
      "api/adrbrowsiel_together_api.h",
    ]

    deps += [
      "//adrbrowsiel/components/adrbrowsiel_together/browser",
      "//adrbrowsiel/components/ntp_widget_utils/browser",
    ]
  }
  if (crypto_dot_com_enabled) {
    sources += [
      "api/crypto_dot_com_api.cc",
      "api/crypto_dot_com_api.h",
    ]
    deps += [
      "//adrbrowsiel/browser/profiles",
      "//adrbrowsiel/common",
      "//adrbrowsiel/components/crypto_dot_com/browser",
      "//adrbrowsiel/components/crypto_dot_com/common/",
      "//adrbrowsiel/components/ntp_widget_utils/browser",
      "//chrome/browser/extensions",
      "//components/prefs",
      "//extensions/browser",
      "//extensions/common:common_constants",
    ]
  }
}

group("resources") {
  deps = [
    "//adrbrowsiel/components/adrbrowsiel_extension:generated_resources",
    "//adrbrowsiel/components/adrbrowsiel_extension:static_resources",
  ]
  if (adrbrowsiel_rewards_enabled) {
    deps += [
      "//adrbrowsiel/components/adrbrowsiel_rewards/resources/extension:extension_generated_resources",
      "//adrbrowsiel/components/adrbrowsiel_rewards/resources/extension:static_resources",
    ]
  }
  if (enable_adrbrowsiel_webtorrent) {
    deps += [
      "//adrbrowsiel/components/adrbrowsiel_webtorrent:generated_resources",
      "//adrbrowsiel/components/adrbrowsiel_webtorrent:static_resources",
    ]
  }
}

config("adrbrowsiel_services_config") {
  defines = [ "adrbrowsiel_SERVICES_KEY=\"$adrbrowsiel_services_key\"" ]
}

config("infura_config") {
  defines = [ "adrbrowsiel_INFURA_PROJECT_ID=\"$adrbrowsiel_infura_project_id\"" ]
}
