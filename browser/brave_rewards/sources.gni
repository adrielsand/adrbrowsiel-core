# Copyright (c) 2021 The adrbrowsiel Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at http://mozilla.org/MPL/2.0/.

import("//adrbrowsiel/components/adrbrowsiel_rewards/browser/buildflags/buildflags.gni")
import("//adrbrowsiel/components/greaselion/browser/buildflags/buildflags.gni")
import("//adrbrowsiel/components/ipfs/buildflags/buildflags.gni")
import("//extensions/buildflags/buildflags.gni")

adrbrowsiel_browser_adrbrowsiel_rewards_sources = []
adrbrowsiel_browser_adrbrowsiel_rewards_deps = []

if (adrbrowsiel_rewards_enabled) {
  adrbrowsiel_browser_adrbrowsiel_rewards_sources += [
    "//adrbrowsiel/browser/adrbrowsiel_rewards/rewards_service_factory.cc",
    "//adrbrowsiel/browser/adrbrowsiel_rewards/rewards_service_factory.h",
    "//adrbrowsiel/browser/adrbrowsiel_rewards/rewards_tab_helper.cc",
    "//adrbrowsiel/browser/adrbrowsiel_rewards/rewards_tab_helper.h",
  ]

  adrbrowsiel_browser_adrbrowsiel_rewards_deps += [
    "//base",
    "//adrbrowsiel/browser/profiles",
    "//adrbrowsiel/browser/profiles:util",
    "//adrbrowsiel/components/adrbrowsiel_rewards/browser",
    "//adrbrowsiel/components/adrbrowsiel_rewards/common",
    "//adrbrowsiel/components/ipfs/buildflags",
    "//adrbrowsiel/vendor/bat-native-ledger:headers",
    "//chrome/browser:browser_process",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui",
    "//components/keyed_service/content",
    "//components/prefs",
    "//components/sessions",
    "//extensions/buildflags",
  ]

  if (enable_greaselion) {
    adrbrowsiel_browser_adrbrowsiel_rewards_deps +=
        [ "//adrbrowsiel/components/greaselion/browser" ]
  }

  if (enable_extensions) {
    adrbrowsiel_browser_adrbrowsiel_rewards_sources += [
      "//adrbrowsiel/browser/adrbrowsiel_rewards/extension_rewards_notification_service_observer.cc",
      "//adrbrowsiel/browser/adrbrowsiel_rewards/extension_rewards_notification_service_observer.h",
      "//adrbrowsiel/browser/adrbrowsiel_rewards/extension_rewards_service_observer.cc",
      "//adrbrowsiel/browser/adrbrowsiel_rewards/extension_rewards_service_observer.h",
    ]

    adrbrowsiel_browser_adrbrowsiel_rewards_deps += [
      "//adrbrowsiel/common/extensions/api",
      "//extensions/browser",
    ]
  }

  if (ipfs_enabled) {
    adrbrowsiel_browser_adrbrowsiel_rewards_deps += [ "//adrbrowsiel/components/ipfs" ]
  }

  if (is_android) {
    adrbrowsiel_browser_adrbrowsiel_rewards_sources += [
      "//adrbrowsiel/browser/adrbrowsiel_rewards/android/adrbrowsiel_rewards_native_worker.cc",
      "//adrbrowsiel/browser/adrbrowsiel_rewards/android/adrbrowsiel_rewards_native_worker.h",
    ]

    adrbrowsiel_browser_adrbrowsiel_rewards_deps += [
      "//adrbrowsiel/browser/adrbrowsiel_ads",
      "//adrbrowsiel/components/adrbrowsiel_ads/browser",
    ]
  } else {
    adrbrowsiel_browser_adrbrowsiel_rewards_sources += [
      "//adrbrowsiel/browser/adrbrowsiel_rewards/tip_dialog.cc",
      "//adrbrowsiel/browser/adrbrowsiel_rewards/tip_dialog.h",
    ]

    adrbrowsiel_browser_adrbrowsiel_rewards_deps += [
      "//adrbrowsiel/common",
      "//components/guest_view/browser",
      "//components/web_modal",
      "//ui/web_dialogs",
    ]
  }
}

if (!is_android) {
  adrbrowsiel_browser_adrbrowsiel_rewards_deps +=
      [ "//adrbrowsiel/components/adrbrowsiel_rewards/common:features" ]
}
