import("//adrbrowsiel/build/config.gni")
import("//build/config/android/rules.gni")

source_set("android_browser_process") {
  check_includes = false

  sources = [
    "adrbrowsiel_feature_list.cc",
    "adrbrowsiel_relaunch_utils.cc",
    "adrbrowsiel_shields_content_settings.cc",
    "adrbrowsiel_shields_content_settings.h",
    "adrbrowsiel_stats_updater_android.cc",
  ]

  deps = [
    "//base",
    "//adrbrowsiel/browser/android/preferences",
    "//adrbrowsiel/browser/adrbrowsiel_stats:stats_updater",
    "//adrbrowsiel/build/android:jni_headers",
    "//adrbrowsiel/components/adrbrowsiel_shields/browser",
    "//chrome/android:jni_headers",
    "//chrome/common",
    "//components/content_settings/core/browser",
    "//components/flags_ui",
    "//components/ntp_tiles",
    "//components/translate/core/browser",
    "//url",
  ]

  if (adrbrowsiel_ads_enabled) {
    sources +=
        [ "//adrbrowsiel/browser/adrbrowsiel_ads/android/adrbrowsiel_ads_native_helper.cc" ]

    deps += [
      "//adrbrowsiel/browser/adrbrowsiel_ads/android:jni_headers",
      "//adrbrowsiel/components/adrbrowsiel_ads/browser",
      "//adrbrowsiel/components/l10n/browser",
    ]
  }

  if (enable_adrbrowsiel_sync) {
    sources += [
      "//adrbrowsiel/browser/sync/adrbrowsiel_sync_devices_android.cc",
      "//adrbrowsiel/browser/sync/adrbrowsiel_sync_devices_android.h",
      "adrbrowsiel_sync_worker.cc",
      "adrbrowsiel_sync_worker.h",
    ]
    deps += [
      "//adrbrowsiel/components/adrbrowsiel_sync",
      "//adrbrowsiel/components/adrbrowsiel_sync:crypto",
      "//adrbrowsiel/components/adrbrowsiel_sync:prefs",
      "//adrbrowsiel/components/adrbrowsiel_sync:profile_sync_service_helper",
      "//components/sync",
      "//components/sync_device_info",
      "//components/unified_consent",
      "//third_party/leveldatabase",
    ]
  }
}
