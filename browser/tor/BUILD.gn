import("//adrbrowsiel/components/adrbrowsiel_ads/browser/buildflags/buildflags.gni")
import("//adrbrowsiel/components/tor/buildflags/buildflags.gni")
import("//extensions/buildflags/buildflags.gni")

source_set("tor") {
  # Remove when https://github.com/adrbrowsiel/adrbrowsiel-browser/issues/10655 is resolved
  check_includes = false
  visibility = [
    ":*",
    "//adrbrowsiel/browser",
    "//adrbrowsiel/browser/decentralized_dns/test:unit_tests",
    "//adrbrowsiel/browser/ipfs/test:*",
    "//adrbrowsiel/browser/profiles",
    "//adrbrowsiel/browser/ui",
    "//adrbrowsiel/test:*",
    "//chrome/browser",
  ]

  if (enable_tor) {
    sources = [
      "onion_location_navigation_throttle_delegate.cc",
      "onion_location_navigation_throttle_delegate.h",
      "tor_profile_manager.cc",
      "tor_profile_manager.h",
      "tor_profile_service_factory.cc",
      "tor_profile_service_factory.h",
    ]

    deps = [
      "//base",
      "//adrbrowsiel/browser/translate/buildflags",
      "//adrbrowsiel/common:pref_names",
      "//adrbrowsiel/components/adrbrowsiel_webtorrent/browser/buildflags",
      "//adrbrowsiel/components/tor",
      "//adrbrowsiel/components/tor:pref_names",
      "//chrome/common",
      "//components/keyed_service/content",
      "//components/safe_browsing/core/common:safe_browsing_prefs",
      "//components/translate/core/browser",
      "//content/public/browser",
      "//third_party/blink/public/common",
    ]

    # Below dep list are not directly used tor target.
    # Only added to fix intermittent build failure.
    # Most of the case comes from including adrbrowsiel_browser_process_impl.h`
    # headers. That header includes some buildflags but sometimes they are
    # not generated before evaluating tor target.
    deps += [
      "//adrbrowsiel/components/adrbrowsiel_referrals/buildflags",
      "//adrbrowsiel/components/greaselion/browser/buildflags",
      "//adrbrowsiel/components/speedreader:buildflags",
    ]
  }
}

source_set("unit_tests") {
  testonly = true
  if (enable_tor) {
    sources = [
      "tor_navigation_throttle_unittest.cc",
      "tor_profile_manager_unittest.cc",
    ]

    deps = [
      "//base",
      "//base/test:test_support",
      "//adrbrowsiel/browser/tor",
      "//adrbrowsiel/browser/translate/buildflags",
      "//adrbrowsiel/common:pref_names",
      "//adrbrowsiel/components/adrbrowsiel_webtorrent/browser/buildflags",
      "//adrbrowsiel/components/tor",
      "//adrbrowsiel/components/tor:test_support",
      "//chrome/common",
      "//chrome/test:test_support",
      "//components/keyed_service/content",
      "//components/safe_browsing/core/common:safe_browsing_prefs",
      "//components/translate/core/browser",
      "//content/public/browser",
      "//content/test:test_support",
      "//testing/gtest",
      "//third_party/blink/public/common",
      "//url",
    ]
  }
}

source_set("browser_tests") {
  testonly = true
  if (enable_tor) {
    sources = [
      "adrbrowsiel_local_state_browsertest.cc",
      "adrbrowsiel_tor_client_updater_browsertest.cc",
      "onion_location_navigation_throttle_browsertest.cc",
      "tor_profile_manager_browsertest.cc",
    ]

    deps = [
      "//base",
      "//adrbrowsiel/app:adrbrowsiel_generated_resources_grit",
      "//adrbrowsiel/browser",
      "//adrbrowsiel/browser/tor",
      "//adrbrowsiel/browser/ui",
      "//adrbrowsiel/common",
      "//adrbrowsiel/components/ipfs/buildflags",
      "//adrbrowsiel/components/tor",
      "//adrbrowsiel/components/tor:pref_names",
      "//adrbrowsiel/components/tor:test_support",
      "//chrome/browser",
      "//chrome/browser:browser_process",
      "//chrome/test:test_support",
      "//chrome/test:test_support_ui",
      "//components/bookmarks/browser",
      "//components/bookmarks/common",
      "//content/public/browser",
      "//content/test:test_support",
      "//net:test_support",
      "//ui/views:test_support",
    ]

    if (enable_extensions) {
      deps += [
        "//extensions/browser",
        "//extensions/common",
      ]
    }

    if (adrbrowsiel_ads_enabled) {
      deps += [
        "//adrbrowsiel/browser/adrbrowsiel_ads",
        "//adrbrowsiel/components/adrbrowsiel_ads/browser",
      ]
    }

    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  }
}
