import("//adrbrowsiel/components/ipfs/buildflags/buildflags.gni")
import("//extensions/buildflags/buildflags.gni")

source_set("browsing_data") {
  # Remove when https://github.com/adrielsand/adrbrowsiel-browser/issues/10657 is resolved
  check_includes = false
  sources = [
    "adrbrowsiel_browsing_data_remover_delegate.cc",
    "adrbrowsiel_browsing_data_remover_delegate.h",
    "adrbrowsiel_clear_browsing_data.cc",
    "adrbrowsiel_clear_browsing_data.h",
  ]

  deps = [
    "//base",
    "//adrbrowsiel/components/ipfs/buildflags",
    "//chrome/browser/browsing_data:constants",
    "//chrome/common",
    "//components/browsing_data/core",
    "//components/content_settings/core/browser",
    "//components/content_settings/core/common",
    "//components/prefs",
    "//content/public/browser",
  ]

  if (enable_extensions) {
    deps += [
      "//adrbrowsiel/common/extensions/api",
      "//extensions/browser",
    ]
  }

  if (ipfs_enabled) {
    deps += [ "//adrbrowsiel/components/ipfs" ]
  }
}

if (!is_android) {
  source_set("browser_tests") {
    testonly = true

    sources = [
      "//adrbrowsiel/browser/browsing_data/adrbrowsiel_clear_browsing_data_browsertest.cc",
    ]

    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

    deps = [
      "//base",
      "//base/test:test_support",
      "//adrbrowsiel/browser/browsing_data",
      "//chrome/app:command_ids",
      "//chrome/browser",
      "//chrome/browser/browsing_data:constants",
      "//chrome/browser/ui",
      "//chrome/common:constants",
      "//chrome/test:test_support",
      "//chrome/test:test_support_ui",
      "//components/browsing_data/core",
      "//components/prefs",
      "//content/public/browser",
      "//content/public/common",
      "//content/test:test_support",
    ]
  }
}
