# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at http://mozilla.org/MPL/2.0/.

import("//adrbrowsiel/components/adrbrowsiel_ads/browser/buildflags/buildflags.gni")
import("//adrbrowsiel/components/adrbrowsiel_rewards/browser/buildflags/buildflags.gni")
import("//adrbrowsiel/components/adrbrowsiel_webtorrent/browser/buildflags/buildflags.gni")
import("//adrbrowsiel/components/speedreader/buildflags.gni")
import("//adrbrowsiel/components/tor/buildflags/buildflags.gni")
import("//build/config/locales.gni")
import("//chrome/common/features.gni")
import("//extensions/buildflags/buildflags.gni")
import("//tools/grit/repack.gni")
import("//ui/base/ui_features.gni")

# Generates a rule to repack a set of resources, substituting a given string
# in for the percentage (e.g. "100", "200"). It generates the repacked files in
# the "gen" directory, and then introduces a copy rule to copy it to the root
# build directory.
#
# Argument:
#   percent [required]
#      String to substitute for the percentage.
template("adrbrowsiel_repack_percent") {
  percent = invoker.percent

  repack(target_name) {
    forward_variables_from(invoker,
                           [
                             "copy_data_to_bundle",
                             "repack_whitelist",
                             "visibility",
                           ])

    # All sources should also have deps for completeness.
    sources =
        [ "$root_gen_dir/adrbrowsiel/adrbrowsiel_theme_resources_${percent}_percent.pak" ]

    deps = [ "//adrbrowsiel/app/theme:adrbrowsiel_theme_resources" ]

    if (defined(invoker.deps)) {
      deps += invoker.deps
    }

    output = "${invoker.output_dir}/adrbrowsiel_${percent}_percent.pak"
  }
}

template("adrbrowsiel_extra_paks") {
  repack(target_name) {
    forward_variables_from(invoker,
                           [
                             "copy_data_to_bundle",
                             "repack_whitelist",
                             "visibility",
                           ])
    output = "${invoker.output_dir}/adrbrowsiel_resources.pak"
    sources = [
      "$root_gen_dir/adrbrowsiel/ui/webui/resources/adrbrowsiel_webui_resources.pak",
      "$root_gen_dir/components/adrbrowsiel_components_resources.pak",
    ]

    if (!is_android) {
      sources += [
        "$root_gen_dir/adrbrowsiel/adrbrowsiel_generated_resources.pak",
        "$root_gen_dir/adrbrowsiel/adrbrowsiel_unscaled_resources.pak",
        "$root_gen_dir/adrbrowsiel/browser/resources/settings/adrbrowsiel_settings_resources.pak",
      ]
    }

    deps = [
      "//adrbrowsiel/components/resources",
      "//adrbrowsiel/ui/webui/resources",
    ]

    if (!is_android) {
      deps += [
        "//adrbrowsiel/app:adrbrowsiel_generated_resources_grit",
        "//adrbrowsiel/app/theme:adrbrowsiel_unscaled_resources",
        "//adrbrowsiel/browser/resources/settings:resources",
        "//adrbrowsiel/components/resources:strings",
      ]
    }

    if (adrbrowsiel_ads_enabled) {
      sources += [ "$root_gen_dir/adrbrowsiel/components/adrbrowsiel_ads/resources/adrbrowsiel_ads_resources.pak" ]

      deps += [ "//adrbrowsiel/components/adrbrowsiel_ads/resources" ]
    }

    if (adrbrowsiel_rewards_enabled) {
      sources += [ "$root_gen_dir/adrbrowsiel/components/adrbrowsiel_rewards/resources/adrbrowsiel_rewards_resources.pak" ]

      deps += [ "//adrbrowsiel/components/adrbrowsiel_rewards/resources" ]
    }

    if (enable_tor) {
      sources +=
          [ "$root_gen_dir/adrbrowsiel/components/tor/resources/tor_resources.pak" ]

      deps += [ "//adrbrowsiel/components/tor/resources" ]
    }

    if (enable_adrbrowsiel_webtorrent) {
      sources += [ "$root_gen_dir/adrbrowsiel/components/adrbrowsiel_webtorrent/adrbrowsiel_webtorrent_resources.pak" ]

      deps += [ "//adrbrowsiel/components/adrbrowsiel_webtorrent:resources" ]
    }

    if (enable_extensions) {
      sources += [
        "$root_gen_dir/adrbrowsiel/browser/resources/extensions/adrbrowsiel_extensions_resources.pak",
        "$root_gen_dir/adrbrowsiel/components/adrbrowsiel_extension/adrbrowsiel_extension_resources.pak",
      ]

      deps += [
        "//adrbrowsiel/browser/resources/extensions:resources",
        "//adrbrowsiel/common/extensions/api",
        "//adrbrowsiel/components/adrbrowsiel_extension:resources",
      ]
    }

    if (adrbrowsiel_rewards_enabled) {
      if (enable_extensions) {
        sources += [ "$root_gen_dir/adrbrowsiel/components/adrbrowsiel_rewards/resources/extension/adrbrowsiel_rewards_extension_resources.pak" ]

        deps +=
            [ "//adrbrowsiel/components/adrbrowsiel_rewards/resources/extension:resources" ]
      }
    }

    if (defined(invoker.deps)) {
      deps += invoker.deps
    }
    if (defined(invoker.additional_paks)) {
      sources += invoker.additional_paks
    }
  }
}

# Defines repack() targets used by adrbrowsiel. Specifically:
# * adrbrowsiel_100_percent.pak
# * adrbrowsiel_200_percent.pak (optionally)
# * resources.pak
#
# Paramters:
#   output_dir [required]: Directory to output .pak files.
#   additional_extra_paks: List of extra .pak sources for resources.pak.
#   locale_whitelist: if set, override repack_whitelist for locale .pak files.
#   copy_data_to_bundle:
#   deps:
#   output_dir:
#   public_deps:
#   repack_whitelist:
#   visibility:
#     Normal meanings.
#
template("adrbrowsiel_paks") {
  adrbrowsiel_repack_percent("${target_name}_100_percent") {
    percent = "100"
    forward_variables_from(invoker,
                           [
                             "copy_data_to_bundle",
                             "deps",
                             "output_dir",
                             "repack_whitelist",
                             "visibility",
                           ])
  }

  if (enable_hidpi) {
    adrbrowsiel_repack_percent("${target_name}_200_percent") {
      percent = "200"
      forward_variables_from(invoker,
                             [
                               "copy_data_to_bundle",
                               "deps",
                               "output_dir",
                               "repack_whitelist",
                               "visibility",
                             ])
    }
  }

  adrbrowsiel_extra_paks("${target_name}_extra") {
    forward_variables_from(invoker,
                           [
                             "copy_data_to_bundle",
                             "deps",
                             "output_dir",
                             "repack_whitelist",
                             "visibility",
                           ])
    if (defined(invoker.additional_extra_paks)) {
      additional_paks = invoker.additional_extra_paks
    }
  }

  group(target_name) {
    forward_variables_from(invoker, [ "deps" ])
    public_deps = [
      ":${target_name}_100_percent",
      ":${target_name}_extra",
    ]
    if (enable_hidpi) {
      public_deps += [ ":${target_name}_200_percent" ]
    }
    if (defined(invoker.public_deps)) {
      public_deps += invoker.public_deps
    }
  }
}
